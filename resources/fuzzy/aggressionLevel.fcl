FUNCTION_BLOCK getAggressionLevel // Block definition

	// distance => distance from player
	// allies => number of allies in close proximity
	VAR_INPUT
		distance : REAL;
	 	allies : REAL;
	END_VAR
	
	// aggression = likely hood of character attacking the player or running to nearby allies
	VAR_OUTPUT
	 	aggression : REAL;
	END_VAR	
	
	// close => 0 - 15 units away
	// moderate => 12.5 - 45 units away
	//far => 40 - 100 units away
	FUZZIFY distance
 		TERM close := (0,0) (5,1) (10,1) (15,0);
 		TERM moderate := trian 12.5 30 45;
 		TERM far := (40,0) (80,1) (100,1);
	END_FUZZIFY
	
	// low => 0 - 2
	// average => 1 - 3
	// high => 2 - 6
	FUZZIFY allies
	 	TERM low := (0,0) (1,1) (2,0);
	 	TERM average := (1,0) (2,1) (3,0);
	 	TERM high := (2,0) (3,1) (6,1);
	END_FUZZIFY
	
	// cowardly => 0 - 20
	// moderate => 15 - 70
	// angry => 60 - 100
	// method => center of gravity
	DEFUZZIFY aggression
		 TERM cowardly := (0, 1) (10, 1) (20, 0);
		 TERM moderate := trian 15 45 70;
		 TERM Angry := (60, 0) (80, 1) (100, 1);
		 METHOD : COG;
	 	 DEFAULT := 5;
	 END_DEFUZZIFY
	 
	 RULEBLOCK No1
		 AND : MIN;
		 ACT : MIN;
		 ACCU : MAX;
		 
		 RULE 1 : IF distance IS far AND allies IS low THEN aggression IS cowardly;
		 RULE 2 : IF distance IS moderate OR allies IS average THEN aggression IS moderate;
		 RULE 3 : IF distance IS close OR allies IS high THEN aggression IS Angry;
		 
	 END_RULEBLOCK
	 
END_FUNCTION_BLOCK